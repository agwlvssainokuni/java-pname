plugins {
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'org.springframework.boot' version '3.5.4' apply false
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    if (project.name in ['pname-cli', 'pname-web']) {
        apply plugin: 'org.springframework.boot'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        jvmArgs += "-javaagent:${configurations.javaagent.asPath}"
    }

    repositories { mavenCentral() }

    configurations { javaagent }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.4'
        }
        dependencies {
            dependency 'com.google.guava:guava:33.4.8-jre'
            dependency 'org.apache.commons:commons-csv:1.14.1'
        }
    }

    dependencies {
        if (project.name == 'pname-main') {
            implementation 'org.springframework.boot:spring-boot-starter'
            implementation 'org.springframework.boot:spring-boot-starter-aop'
            implementation 'com.google.guava:guava'
            implementation 'org.apache.commons:commons-lang3'
            implementation 'org.apache.commons:commons-csv'
            implementation 'com.fasterxml.jackson.core:jackson-databind'
            javaagent('org.mockito:mockito-core') { transitive = false }
        }
        if (project.name == 'pname-cli') {
            implementation project(':pname-main')
            implementation 'org.springframework.boot:spring-boot-starter'
            implementation 'org.apache.commons:commons-lang3'
            implementation 'org.apache.commons:commons-csv'
        }
        if (project.name == 'pname-web') {
            implementation project(':pname-main')
            implementation 'org.springframework.boot:spring-boot-starter-web'
            implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
            implementation 'org.apache.commons:commons-lang3'
            implementation 'org.apache.commons:commons-csv'
        }
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
}
